[Открытая платформа для создания заботливой квартиры](http://cutecare.ru)

В этом сценарии мы реализуем дистанционное управление освещением, с обратной связью, интегрированное в инфраструктуру заботливой квартиры, а именно:

* включение/отключение освещения через интерфейс Home Assistant;
* включение/отключение освещения дистанционно при помощи сенсорного выключателя;
* измерение электроэнергии, потребленной светильником.

Итак, для автоматизации освещения вам потребуются эти компоненты:

|Название|Назначение|Цена, руб.|
| :----------- |:----------- |:----------- |
|[BLE JDY-08](https://rover.ebay.com/rover/1/711-53200-19255-0/1?icep_id=114&ipn=icep&toolid=20004&campid=5338218090&mpre=https%3A%2F%2Fwww.ebay.com%2Fitm%2FBluetooth-4-0-BLE-Low-Power-CC2541-JDY-08-Support-Airsync-iBeacon-Module%2F322511962233%3FssPageName%3DSTRK%253AMEBIDX%253AIT%26_trksid%3Dp2057872.m2749.l2649)|Радиомодуль Bluetooth LE|140|
|[Arduino Nano](https://www.ebay.com/itm/Nano-V3-0-ATmega328P-CH340G-5V-16M-MINI-USB-Nano-V3-0-Compatible-to-Arduino/253141071056?ssPageName=STRK%3AMEBIDX%3AIT&_trksid=p2057872.m2749.l2649)|Микроконтроллер для снятия показаний датчика и управления реле|180|
|[Hi-Link HLK PM-01 5V](https://rover.ebay.com/rover/1/711-53200-19255-0/1?icep_id=114&ipn=icep&toolid=20004&campid=5338218090&mpre=https%3A%2F%2Fwww.ebay.com%2Fitm%2FHLK-PM03-AC-DC-220V-to-3-3V-Step-Down-Buck-Isolated-Power-Supply-Module%2F311759562967%3FssPageName%3DSTRK%253AMEBIDX%253AIT%26_trksid%3Dp2057872.m2749.l2649)|Изолированный модуль питания 5В|150|
|[G3MB-202P DC-AC 5V](https://rover.ebay.com/rover/1/711-53200-19255-0/1?icep_id=114&ipn=icep&toolid=20004&campid=5338218090&mpre=https%3A%2F%2Fwww.ebay.com%2Fitm%2F1-2-5-10PCS-5V-12V-24V-G3MB-202P-DC-AC-PCB-SSR-Solid-State-Relay-Module%2F202056147875%3FssPageName%3DSTRK%253AMEBIDX%253AIT%26var%3D502051903070%26_trksid%3Dp2057872.m2749.l2649)|Твердотельное реле|56|
|[ACS712](https://rover.ebay.com/rover/1/711-53200-19255-0/1?icep_id=114&ipn=icep&toolid=20004&campid=5338218090&mpre=https%3A%2F%2Fwww.ebay.com%2Fitm%2F5A-range-Current-Sensor-Module-ACS712-Module-M61-Top%2F222086976642%3FssPageName%3DSTRK%253AMEBIDX%253AIT%26_trksid%3Dp2057872.m2749.l2649)|Модуль измерения силы тока в цепи|90|
|[MX-05V](https://amperkot.ru/products/radiomoduli_433mhz_peredatchik_mxfs03v_i_priemnik_mx05v/23869814.html)|Радиоприемник (433 МГц), продается вместе с передатчиком|80|

Собранное устройство состоит из двух частей: силовой (блок питания, реле, модуль измерения силы тока) и логики (микроконтроллер, BLE-модуль, радиоприемник). Устройство легко размещается в потолочной чаши светильника.

![Светильник подключенный к умной квартире](https://github.com/cutecare/cutecare-docs/blob/master/images/RemoteLight.jpg?raw=true)

Чтобы детям было удобнее включать/выключать освещение, соберите сенсорный дистанционный выключатель, работающий от двух батареек CR2032. Для этого вам потребуются следующие компоненты:

|Название|Назначение|Цена, руб.|
| :----------- |:----------- |:----------- |
|[Arduino Pro Mini](https://rover.ebay.com/rover/1/711-53200-19255-0/1?icep_id=114&ipn=icep&toolid=20004&campid=5338218090&mpre=https%3A%2F%2Fwww.ebay.com%2Fitm%2F2PCS-New-Pro-Mini-atmega328-Board-5V-16M-Arduino-Compatible-Nano%2F191674251828%3FssPageName%3DSTRK%253AMEBIDX%253AIT%26_trksid%3Dp2057872.m2749.l2649)|Микроконтроллер для  считывания касания и передачи сигнала в светильник|135|
|[MX-FS-03V](https://amperkot.ru/products/radiomoduli_433mhz_peredatchik_mxfs03v_i_priemnik_mx05v/23869814.html)|Радиопередатчик (433 МГц), продается вместе с радиоприемником|0|
|[TTP223](https://rover.ebay.com/rover/1/711-53200-19255-0/1?icep_id=114&ipn=icep&toolid=20004&campid=5338218090&mpre=https%3A%2F%2Fwww.ebay.com%2Fitm%2F10PCS-TTP223-Capacitive-Touch-Switch-Button-Self-Lock-Module-Sensor-for-Arduino%2F222726613292%3FssPageName%3DSTRK%253AMEBIDX%253AIT%26_trksid%3Dp2057872.m2749.l2649)|Сенсор прикосновения|9|

Собранное устройство удобно разместить в кнопке-светильнике, предназначенной для установки в шкафах, которую можно купить в любом гипер-маркете. Например, у вас может получиться что-то подобное:

![Сенсорный дистанционный выключатель](https://github.com/cutecare/cutecare-docs/blob/master/images/SensorSwitchDevice.jpg?raw=true)

**Программируем светильник**

О том как настроить Arduino IDE и подключить микроконтроллер к ПК читайте в этой коротенькой [инструкции](http://cutecare.readthedocs.io/ru/master/%D0%9C%D0%B8%D0%BA%D1%80%D0%BE%D0%BA%D0%BE%D0%BD%D1%82%D1%80%D0%BE%D0%BB%D0%BB%D0%B5%D1%80%D1%8B/#arduino-nano). Для компиляции программы необходимо загрузить библиотеки и добавить их в Arduino IDE через меню Sketch - Install library:

* [JDY8](https://github.com/cutecare/jdy8/archive/master.zip)
* [RCSwitch](https://github.com/cutecare/rc-switch/archive/master.zip)
* [ACSensor](https://github.com/cutecare/ACS/archive/master.zip)

```
#include <jdy8.h>
#include <LowPower.h>
#include <RCSwitch.h>
#include <ACSensor.h>

#define RC_PIN 2
#define RELEY_PIN 4
#define BLE_IOPIN 5
#define RC_COMMAND 5121
#define AC_SENSOR_PIN A1

JDY8 ble(3, 7, 10); // RX, TX, Reset
RCSwitch sensorButton = RCSwitch();
ACSensor currentSensor(AC_SENSOR_PIN, 185.0, 9.0); // sensor pin, ACS sensitivity (mV/A), ACS noise level (mV)
unsigned int lastValue = 0;
bool buttonState = false;

void setup()
{
  // get zero of AC sensor
  currentSensor.calibrate();

  // turn on light when power is on
  pinMode(RELEY_PIN, OUTPUT);
  digitalWrite(RELEY_PIN, HIGH);

  ble.configure("Light-1", 0, 1);
  ble.major(1, false);
 
  // listen for sensor button
  sensorButton.enableReceive(0);
}

void loop()
{
  if (sensorButton.available()) {
    if ( sensorButton.getReceivedValue() == RC_COMMAND ) {
      buttonState = !buttonState;
      digitalWrite(RELEY_PIN, buttonState ? HIGH : LOW);
      ble.setIOPin(2, buttonState ? 1 : 0, false);
      delay(2000);
    }
    sensorButton.resetAvailable();
  }
  
  // set relay state based on BLE IO pin state
  pinMode(BLE_IOPIN, INPUT);
  pinMode(RELEY_PIN, OUTPUT);
  digitalWrite(RELEY_PIN, digitalRead(BLE_IOPIN) == HIGH ? HIGH : LOW);

  // read AC sensor value
  unsigned int nowValue = currentSensor.getWatt();
  if ( nowValue == lastValue ) return;
  lastValue = nowValue;

  // configure BLE if sensor values have changed since the last measure
  ble.major(lastValue, false);
}
```

**Собираем по схеме**

![Схема умного управления освещением](https://github.com/cutecare/cutecare-docs/blob/master/images/RemoteLigth_bb.png?raw=true)

**Программируем сенсорный выключатель**

О том как настроить Arduino IDE и подключить микроконтроллер к ПК читайте в этой коротенькой [инструкции](http://cutecare.readthedocs.io/ru/master/%D0%9C%D0%B8%D0%BA%D1%80%D0%BE%D0%BA%D0%BE%D0%BD%D1%82%D1%80%D0%BE%D0%BB%D0%BB%D0%B5%D1%80%D1%8B/#arduino-pro-mini). Для компиляции программы необходимо загрузить библиотеки и добавить их в Arduino IDE через меню Sketch - Install library:

* [RCSwitch](https://github.com/cutecare/rc-switch/archive/master.zip)

```
#include <LowPower.h>
#include <RCSwitch.h>

#define SENSOR_TOUCH_PIN 4
#define LED_PIN 3
#define RC_PIN 12
#define RC_COMMAND 5121

RCSwitch remote = RCSwitch();

void setup() {
  remote.enableTransmit(RC_PIN);
}

void loop() 
{
  pinMode(SENSOR_TOUCH_PIN, INPUT);
  int touch = digitalRead(SENSOR_TOUCH_PIN);

  if ( touch == HIGH ) 
  {
    remote.send(RC_COMMAND, 24);

    pinMode(LED_PIN, OUTPUT);
    fadeIn();
    fadeOut();
    fadeIn();
    fadeOut();
    pinMode(LED_PIN, INPUT);
  }
  
  // fall a sleep
  LowPower.powerDown(SLEEP_500MS, ADC_OFF, BOD_OFF);
}

void fadeIn() {
    for (int fadeValue = 0 ; fadeValue <= 255; fadeValue += 5) {
      analogWrite(LED_PIN, fadeValue);
      delay(15);
    }
}

void fadeOut() {
    for (int fadeValue = 255 ; fadeValue >= 0; fadeValue -= 5) {
      analogWrite(LED_PIN, fadeValue);
      delay(15);
    }
}
```

**Собираем по схеме**

![Схема умного управления освещением](https://github.com/cutecare/cutecare-docs/blob/master/images/RemoteLigth_bb.png?raw=true)


**Программируем HASS**

Файл: /config/configuration.yaml

```
switch:
  - platform: cutecare
    name: Remote Light
    mac: 0C:B2:B7:3E:C3:E5
    threshold: 5
    pin: 2

sensor:
  - platform: cutecare
    scan_interval: 60
    mac: 0C:B2:B7:3E:C3:E5
    monitored_conditions:
      - watt
    name: remote_light
```

Параметр "threshold" позволяет указать границу срабатывания датчика обратной связи.
Параметр "pin" позволяет указать номер пина JDY-08, с которого идет сигнал на реле.

**Дополнительные материалы**

* [Обзор радиопередатчика](http://makerplus.ru/wiki/besprovodnoj-peredatchik-fs1000a-priemnik-mx-rm-5v)
* [Об измерении величины тока при помощи сенсоров ACS](https://www.dfrobot.com/wiki/index.php/50A_Current_Sensor(SKU:SEN0098))
* [Теория измерения величины переменного тока](https://www.dataforth.com/measuring-rms-values.aspx)
